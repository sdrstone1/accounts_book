apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.kollhong.accounts3"
        minSdkVersion 22
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug{
            minifyEnabled false
            crunchPngs false
        }
    }
    flavorDimensions "stage"
    productFlavors {
        beta {
            dimension "stage"
            applicationIdSuffix ".beta"
            versionCode 100
            versionName "1.0.0"
            buildConfigField 'boolean', 'isTEST', "true"
            //Log.d, Toast에 if(BuildConfig.isTEST) 로 분기
            minSdkVersion 22
            resConfigs "ko", "xxxhdpi"
            ext.enableCrashlytics = false
        }
        rel {
            dimension "stage"
            applicationIdSuffix ".rel"
            versionCode new Date().format('yyMMddHHmm').toInteger()
            versionName "1.0.0"
            buildConfigField 'boolean', 'isTEST', "false"
            //Log.d, Toast에 if(BuildConfig.isTEST) 로 분기
            minSdkVersion 22
        }

    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk false
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    variantFilter{ variant ->
        def flavorName = variant.getFlavors().get(0).name
        def buildName = variant.buildType.name;
        splits.abi.enable = (flavorName == 'rel')
        if (flavorName == 'beta' && buildName == 'release' || flavorName == 'rel' && buildName == 'debug') {
            variant.setIgnore(true);
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:support-vector-drawable:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'
    implementation 'com.github.hadiidbouk:ChartProgressBar-Android:2.0.6'
    implementation 'com.github.prolificinteractive:material-calendarview:1.6.1'

}
